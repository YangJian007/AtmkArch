apply plugin: 'com.android.application'
apply from: '../common.gradle'


// Android 代码规范文档：https://github.com/getActivity/AndroidCodeStandard
android {

    // 资源目录存放指引：https://developer.android.google.cn/guide/topics/resources/providing-resources
    defaultConfig {
        // 无痛修改包名：https://www.jianshu.com/p/17327e191d2e
        applicationId 'com.atmk.iot'
        // 仅保留中文语种的资源
        resConfigs 'zh'
        // 仅保留 xxhdpi 图片资源（目前主流分辨率 1920 * 1080）
        resConfigs 'xxhdpi'
        // 混淆配置
        proguardFiles 'proguard-sdk.pro', 'proguard-app.pro'

    }

    // Apk 签名的那些事：https://www.jianshu.com/p/a1f8e5896aa2
    signingConfigs {
        config {
            storeFile file(StoreFile)
            storePassword StorePassword
            keyAlias KeyAlias
            keyPassword KeyPassword
        }
    }



    // 构建配置：https://developer.android.google.cn/studio/build/build-variants
    buildTypes {

        debug {
            // 给包名添加后缀
//            applicationIdSuffix '.debug'
            // 调试模式开关
            debuggable true
            jniDebuggable true
//            // 压缩对齐开关
            zipAlignEnabled false
//            // 移除无用的资源
            shrinkResources false
            // 代码混淆开关
            minifyEnabled false
            // 签名信息配置
            signingConfig signingConfigs.config
            // 添加清单占位符
            addManifestPlaceholders([
                    'app_name': '物联网平台 Debug 版'
            ])
            // 调试模式下只保留一种架构的 so 库，提升打包速度
            ndk {
                abiFilters 'armeabi-v7a'
            }
        }


        release {
            // 调试模式开关
            debuggable false
            jniDebuggable false
//            // 压缩对齐开关  放开影响aop
//            zipAlignEnabled true
//            // 移除无用的资源   放开影响aop
//            shrinkResources true
            // 代码混淆开关   放开影响aop
//            minifyEnabled true
            // 签名信息配置
            signingConfig signingConfigs.config
            // 添加清单占位符
            addManifestPlaceholders([
                    'app_name': '物联网平台'
            ])
            // 仅保留两种架构的 so 库，根据 Bugly 统计得出
            ndk {
                // armeabi：万金油架构平台（占用率：0%）
                // armeabi-v7a：曾经主流的架构平台（占用率：10%）
                // arm64-v8a：目前主流架构平台（占用率：95%）
                abiFilters 'armeabi-v7a', 'arm64-v8a'
            }
        }
    }

    packagingOptions {
        // 剔除这个包下的所有文件（不会移除签名信息）
        exclude 'META-INF/*******'
    }


    applicationVariants.all { variant ->
        // apk 输出文件名配置
        variant.outputs.all { output ->
            outputFileName = project.getName() + '_v' + variant.versionName + '_' + variant.buildType.name
            if (variant.buildType.name == buildTypes.release.getName()) {
                outputFileName += '_' + new Date().format('MMdd')
            }
            outputFileName += '.apk'
        }
    }


}


dependencies {
    kapt 'com.alibaba:arouter-compiler:1.5.2'
    kapt 'com.github.bumptech.glide:compiler:4.12.0'
    //业务基础模块
    implementation project(':bz:bz_base')
    //登录模块
    implementation project(':bz:bz_login')
    //设备模块
    implementation project(':bz:bz_device')
    //统计模块
    implementation project(':bz:bz_statistics')


}